PDefs.   Program ::= [Def] ;
terminator Def "" ;

DFun.        Def ::= "func" Id "(" [Arg] ")" "->" Type "{" [Stm] "}" ;
DFunExposed. Def ::= "exposed" "func" Id "(" [Arg] ")" "->" Type "{" [Stm] "}" ;
DTypedef.	 Def ::= "typedef" Ident "=" "union" Type "|" Type ;

separator  Arg "," ;
terminator Stm "" ;

ADecl.     Arg ::= Id ":" Type ;

SInit.     Stm ::= "let" IdMaybeTyped "=" Exp ;
SSplit.    Stm ::= "let" "(" IdMaybeTyped "," IdMaybeTyped ")" "=" Exp ;
SCase.     Stm ::= "if" "(" "case" IdMaybeTyped "=" Case Exp ")" "{" [Stm] "}" "else" "{" [Stm] "}" ;
SReturn.   Stm ::= "return" Exp ;
SAssert.   Stm ::= Assertion ;

IdNotTyped. IdMaybeTyped ::= Id ;
IdTyped.    IdMaybeTyped ::= Id ":" Type ;

rules Case ::= "inl" | "inr" ;

EInt.    Exp15 ::= Integer ;
EUnit.   Exp15 ::= "(" ")" ;
EId.     Exp15 ::= Id ;
EPair.   Exp15 ::= "(" Exp "," Exp ")" ;
ESum.    Exp15 ::= Ident "." Case "(" Exp ")" ;


EApp.    Exp15 ::= Id "(" [Exp] ")" ;
separator Exp "," ;

internal ETyped. Exp ::= "(" Exp ":" Type ")" ;

coercions Exp 15 ;

ATypeEqual. Assertion ::= "assertTypeEqual" "(" Id "," Type ")" ;

TType. 				 Type ::= CoreType "!" Double ;
TTypeExponential.  	 Type ::= CoreType "!" "inf" ;
TTypeConvenienceInt. Type ::= CoreType "!" Integer ;

CTBase.    CoreType ::= BaseType ;
CTMulPair. CoreType ::= "(" Type "," Type ")" ;
CTSum.     CoreType ::= "(" Type "+" Type ")" ;
CTTypedef. CoreType ::= Ident ;

rules BaseType ::= "Int" | "Unit" ;

token Id (lower (letter | digit | '_')*) ;

comment "//" ;
comment "/*" "*/" ;
