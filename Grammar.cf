PDefs.   Program ::= [Def] ;
terminator Def "" ;

DFun.        Def ::= "func" Id "(" [Arg] ")" "->" Type "{" [Stm] "}" ;
DFunExposed. Def ::= "exposed" "func" Id "(" [Arg] ")" "->" Type "{" [Stm] "}" ;
DTypedef.	 Def ::= "typedef" Ident "=" "union" Type "|" Type ;

separator  Arg "," ;
terminator Stm "" ;

ADecl.     Arg ::= Id ":" Type ;

SInit.     Stm ::= "let" IdMaybeTyped "=" Exp ;
SSplit.    Stm ::= "let" "(" IdMaybeTyped "," IdMaybeTyped ")" "=" Exp ;
SCase.     Stm ::= "if" "(" "case" IdMaybeTyped "=" Case Exp ")" "{" [Stm] "}" "else" "{" [Stm] "}" ;
SReturn.   Stm ::= "return" Exp ;
SAssert.   Stm ::= Assertion ;

IdNotTyped. IdMaybeTyped ::= Id ;
IdTyped.    IdMaybeTyped ::= Id ":" Type ;

rules Case ::= "inl" | "inr" ;

EInt.    Exp15 ::= Integer ;
EFloat.  Exp15 ::= Double ;
EUnit.   Exp15 ::= "(" ")" ;
ETrue.   Exp15 ::= "true" ;
EFalse.  Exp15 ::= "false" ;
EId.     Exp15 ::= Id ;
EPair.   Exp15 ::= "(" Exp "," Exp ")" ;
ESum.    Exp15 ::= Ident "." Case "(" Exp ")" ;

EApp.    Exp15 ::= Id "(" [Exp] ")" ;

ETimes.   Exp12 ::= Exp12 "*"  Exp13 ;
EPlus.    Exp11 ::= Exp11 "+"  Exp12 ;
EMinus.   Exp11 ::= Exp11 "-"  Exp12 ;

ELt.      Exp9  ::= Exp9  "<"  Exp10 ;
EGt.      Exp9  ::= Exp9  ">"  Exp10 ;
ELtEq.    Exp9  ::= Exp9  "<=" Exp10 ;
EGtEq.    Exp9  ::= Exp9  ">=" Exp10 ;

EEq.      Exp8  ::= Exp8  "==" Exp9 ;
ENeq.     Exp8  ::= Exp8  "!=" Exp9 ;

internal ETyped. Exp ::= "(" Exp ":" Type ")" ;

separator Exp "," ;
coercions Exp 15 ;

ATypeEqual. Assertion ::= "assertTypeEqual" "(" Id "," Type ")" ;

TType. 				 Type ::= CoreType "!" Double ;
TTypeExponential.  	 Type ::= CoreType "!" "inf" ;
TTypeConvenienceInt. Type ::= CoreType "!" Integer ;

CTBase.    CoreType ::= BaseType ;
CTMulPair. CoreType ::= "(" Type "," Type ")" ;
CTSum.     CoreType ::= "(" Type "+" Type ")" ;
CTTypedef. CoreType ::= Ident ;
CTFunction.CoreType ::= "(" "(" [Type] ")" "->" Type ")" ;

separator Type "," ;

rules BaseType ::= "Int" | "Float" | "Unit" ;

token Id (lower (letter | digit | '_')*) ;

comment "//" ;
comment "/*" "*/" ;
